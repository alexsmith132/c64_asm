;multipliziere 8 mal 8 bit
;
;Argumente: md*mr
;Rückgabe:  lo:md hi:mr
;
; Weiterverarbeitung des Wertes z.B.
; ldx #$90
; sec
; jsr $bc49 Wandelt Ergebnis nach FAC

md 		= $63
mr		= $62

mul		lda #0			;Setzen des Produkts
		ldx #8			;Zähler (8 Durchläufe)
.mulloop	asl 			;Produkt in A über
		rol mr			;mr nach links schieben
		bcc .mulnext		;höchstes Bit in mr=0
		clc			;Falls höchstes Bit in mr=1
		adc md			;md zum Teilprodukt addieren
		bcc .mulnext		;kein Ertrag
		inc mr			;Ertrag nach mr berücksichtigen
.mulnext	dex
		bne .mulloop		;weiter, falls Zähler noch nicht 0
		sta md
		rts
